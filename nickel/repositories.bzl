"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("@bazel_tools//tools/build_defs/repo:http.bzl", _http_archive = "http_archive", _http_file = "http_file")
load("@bazel_tools//tools/build_defs/repo:utils.bzl", "maybe")
load("//nickel/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")
load("//nickel/private:versions.bzl", "TOOL_VERSIONS")

def http_file(name, **kwargs):
    maybe(_http_file, name = name, **kwargs)

def http_archive(name, **kwargs):
    maybe(_http_archive, name = name, **kwargs)

# WARNING: any changes in this function may be BREAKING CHANGES for users
# because we'll fetch a dependency which may be different from one that
# they were previously fetching later in their WORKSPACE setup, and now
# ours took precedence. Such breakages are challenging for users, so any
# changes in this function should be marked as BREAKING in the commit message
# and released only in semver majors.
# This is all fixed by bzlmod, so we just tolerate it for now.
def rules_nickel_dependencies():
    # The minimal version of bazel_skylib we require
    http_archive(
        name = "bazel_skylib",
        sha256 = "74d544d96f4a5bb630d465ca8bbcfe231e3594e5aae57e1edbf17a6eb3ca2506",
        urls = [
            "https://github.com/bazelbuild/bazel-skylib/releases/download/1.3.0/bazel-skylib-1.3.0.tar.gz",
            "https://mirror.bazel.build/github.com/bazelbuild/bazel-skylib/releases/download/1.3.0/bazel-skylib-1.3.0.tar.gz",
        ],
    )

########
# Remaining content of the file is only used to support toolchains.
########
_DOC = "Fetch external tools needed for Nickel toolchain"
_ATTRS = {
    "nickel_version": attr.string(mandatory = True, values = TOOL_VERSIONS.keys()),
    "platform": attr.string(mandatory = True, values = PLATFORMS.keys()),
}

def _nickel_repo_impl(repository_ctx):
    url = "https://github.com/tweag/nickel/releases/download/{0}/nickel-{1}".format(
        repository_ctx.attr.nickel_version,
        repository_ctx.attr.platform,
    )
    artifact = TOOL_VERSIONS[repository_ctx.attr.nickel_version]["nickel-{0}".format(repository_ctx.attr.platform)]
    repository_ctx.download(
        url = artifact["url"],
        output = "nickel",
        executable = True,
        integrity = artifact["hash"],
    )
    build_content = """#Generated by nickel/repositories.bzl
load("@org_nickel_lang_rules_nickel//nickel:toolchain.bzl", "nickel_toolchain")
nickel_toolchain(name = "nickel_toolchain", nickel = "nickel")
"""
    # Base BUILD file for this repository
    repository_ctx.file("BUILD.bazel", build_content)

nickel_repositories = repository_rule(
    _nickel_repo_impl,
    doc = _DOC,
    attrs = _ATTRS,
)

# Wrapper macro around everything above, this is the primary API
def nickel_register_toolchains(name, nickel_version, register = True, **kwargs):
    """Convenience macro for users which does typical setup.

    - create a repository for each built-in platform like "nickel-x86_64-linux" -
      this repository is lazily fetched when Nickel is needed for that platform.
    - create a repository exposing toolchains for each platform like "nickel_platforms"
    - register a toolchain pointing at each platform
    Users can avoid this macro and do these steps themselves, if they want more control.
    Args:
        name: base name for all created repos, like "nickel1_0_0"
        nickel_version: the specific Nickel version to use, e.g. "1.0.0"
        register: whether to call through to native.register_toolchains.
            Should be True for WORKSPACE users, but false when used under bzlmod extension
        **kwargs: passed to each node_repositories call
    """
    for platform in PLATFORMS.keys():
        nickel_repositories(
            name = name + "_" + platform,
            nickel_version = nickel_version,
            platform = platform,
            **kwargs
        )
        if register:
            native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )
